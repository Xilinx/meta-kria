# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += " \
    ${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend \
    "

BBFILES_DYNAMIC += " \
xilinx-tools:${LAYERDIR}/dynamic-layers/xilinx-tools/recipes-*/*/*.bb \
xilinx-tools:${LAYERDIR}/dynamic-layers/xilinx-tools/recipes-*/*/*.bbappend \
petalinux:${LAYERDIR}/dynamic-layers/petalinux/recipes-*/*/*.bb \
petalinux:${LAYERDIR}/dynamic-layers/petalinux/recipes-*/*/*.bbappend \
"

BBFILE_COLLECTIONS += "kria"
BBFILE_PRIORITY_kria = "5"
BBFILE_PATTERN_kria = "^${LAYERDIR}/"
BBFILE_PATTERN_IGNORE_EMPTY_kria = "1"

LAYERDEPENDS_kria  = "core xilinx"

LAYERRECOMMENDS_kria = "xilinx-tools"

LAYERSERIES_COMPAT_kria = "nanbield"

LAYER_PATH_kria-layer = "${LAYERDIR}"

# Both kria-pwrctl and xmutil share version scheme
KRIA_PWRCTL_VERSION[v2022.1] = "1.0"
KRIA_PWRCTL_VERSION[v2022.2] = "1.0"
KRIA_PWRCTL_VERSION[v2023.1] = "1.1"
PREFERRED_VERSION_kria-pwrctl = "${@d.getVarFlag('KRIA_PWRCTL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_xmutil = "${@d.getVarFlag('KRIA_PWRCTL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_imgrcry-make = "${@d.getVar("XILINX_RELEASE_VERSION").replace('v','')}%"
